plugins {
	id 'org.springframework.boot' version '2.5.2'
	id 'io.spring.dependency-management' version '1.0.11.RELEASE'
	id 'java'
	id 'org.sonarqube' version '3.3'
	id 'jacoco'
}

group = 'com.example'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '15'

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
	
	// Exclude Logback for Log4j2
	implementation.exclude module: 'spring-boot-starter-logging'
}

repositories {
	mavenCentral()
}

dependencies {
	// Spring MongoDB Reactive
	implementation 'org.springframework.boot:spring-boot-starter-data-mongodb-reactive'
	
	// Spring Webflux
	implementation 'org.springframework.boot:spring-boot-starter-webflux'
	
	// Spring Tools
	developmentOnly 'org.springframework.boot:spring-boot-devtools'
	annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'
	
	// Spring Security
	implementation 'org.springframework.boot:spring-boot-starter-security'
	testImplementation 'org.springframework.security:spring-security-test'
	
	// Lombok
	compileOnly 'org.projectlombok:lombok'
	annotationProcessor 'org.projectlombok:lombok'
	
	// Test
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'io.projectreactor:reactor-test'
	
	// Log4j2
	implementation 'org.springframework.boot:spring-boot-starter-log4j2'
	// Log4j2 Async dependency
	implementation group: 'com.lmax', name: 'disruptor', version: '3.4.4'
	
	// Jasypt
	implementation group: 'com.github.ulisesbocchio', name: 'jasypt-spring-boot-starter', version: '3.0.3'
	
	// Apache Commons
	implementation group: 'org.apache.commons', name: 'commons-lang3', version: '3.12.0'
	implementation group: 'org.apache.commons', name: 'commons-collections4', version: '4.4'
	
	// Argon2 dependency
	implementation group: 'org.bouncycastle', name: 'bcprov-jdk15on', version: '1.69'
	
	// Paseto for java
	implementation group: 'dev.paseto', name: 'jpaseto-api', version: '0.6.0'
	implementation group: 'dev.paseto', name: 'jpaseto-impl', version: '0.6.0'
	implementation group: 'dev.paseto', name: 'jpaseto-jackson', version: '0.6.0'
	
}

sonarqube {
	properties {
		property 'sonar.projectKey', 'soranolan_cicd'
		property 'sonar.organization', 'soranolan'
		property 'sonar.host.url', 'https://sonarcloud.io'
	}
}

jacocoTestReport {
	reports {
		xml.enabled true
	}
}

test {
	useJUnitPlatform()
}
